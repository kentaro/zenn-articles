---
title: "Misskeyä¸Šã§Brainf*ckã‚’å®Ÿè¡Œã§ãã‚‹Playã‚’ä½œã‚Šã¾ã—ãŸ"
emoji: "ğŸ”–"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["Misskey", "Brainfuck"]
published: true
---

Resoniteã®æ–¹ã€…ãŒMisskeyã«ã„ã‚‹ã®ã§ã€ã¡ã‚‡ã£ã¨å‰ã‹ã‚‰ãƒ¦ãƒ¼ã‚¶ã¨ã—ã¦Misskeyã‚’ä½¿ã„å§‹ã‚ã¾ã—ãŸã€‚ã¾ãŸã€ãã‚‚ãã‚‚Fediverseã«ã¤ã„ã¦ä½•ã‚‚çŸ¥ã‚‰ãªã„ã“ã¨ã«æ°—ã¥ã„ãŸã®ã§ã€è‡ªåˆ†ã§ã‚‚ã‚µãƒ¼ãƒã‚’ç«‹ã¦ã¦ã¿ãŸã‚Šã€ActivityPubã¾ã‚ã‚Šã«ã¤ã„ã¦èª¿ã¹ãŸã‚Šã—ã¦ã„ã¾ã—ãŸã€‚

Misskeyã«ã¯[AiScript](https://misskey-hub.net/ja/docs/for-developers/aiscript/)ã¨ã„ã†è¨€èªå‡¦ç†ç³»ãŒçµ„ã¿è¾¼ã¾ã‚Œã¦ã„ã¦ã€[ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã‚„ãƒ„ãƒ¼ãƒ«ã‚’ãƒ¦ãƒ¼ã‚¶ãŒè‡ªä½œã§ãã‚‹](https://misskey-hub.net/ja/docs/for-developers/plugin/)ã¨ã„ã†ã“ã¨ã‚’çŸ¥ã‚Šã€èˆˆå¥®ã—ã¦ã„ã¾ã™ã€‚ãã†ãªã‚‹ã¨ã‚„ã£ã±ã‚Šã€ã¨ã‚Šã‚ãˆãšBrainf\*ckã‚’å®Ÿè£…ã—ã¦ã¿ãŸããªã‚Šã¾ã™ã‚ˆã­ã€‚ãã‚“ãªã‚ã‘ã§ã€Misskeyä¸Šã§Brainf\*ckã®ã‚³ãƒ¼ãƒ‰ã‚’å®Ÿè¡Œã§ãã‚‹Playã‚’æ›¸ã„ã¦ã¿ã¾ã—ãŸã€‚

## ä½¿ã„æ–¹

ä»¥ä¸‹ã®å‹•ç”»ã®é€šã‚Šã§ã™ã€‚ãŠå¥½ãã«ã‚³ãƒ¼ãƒ‰ã‚’æ›¸ã„ã¦å®Ÿè¡Œã—ã¦ãã ã•ã„ã€‚

https://x.com/kentaro/status/1745058567005307266

`,`ï¼ˆæ–‡å­—ã®å…¥åŠ›ã‚’ã†ã‘ã¤ã‘ã‚‹å‘½ä»¤ï¼‰ã¯å®Ÿè£…ã—ã¦ã„ã¾ã›ã‚“ï¼ˆå…¥åŠ›ç”¨ã®ãƒ†ã‚­ã‚¹ãƒˆãƒœãƒƒã‚¯ã‚¹ã‚’ç”¨æ„ã™ã‚Œã°ã§ããªãã¯ãªã•ãã†ã§ã™ãŒï¼‰ã€‚

## å®Ÿè£…

ã‚³ãƒ¼ãƒ‰ã¯ä»¥ä¸‹ã®é€šã‚Šã§ã™ã€‚Brainf*ckã®éƒ¨åˆ†ã¯ã€Œ[Brainf*ckã™ã‚‹LINE Botã‚’GASã§ä½œã‚‹](https://zenn.dev/kagamoc/articles/e4c42bccfbed7e)ã€ã‚’å¤§å¹…ã«å‚è€ƒã«ã—ã¾ã—ãŸã€‚ãƒãƒ¼ã‚¸ãƒ§ãƒ³0.16.0ã§å°å…¥ã•ã‚ŒãŸé–¢æ•°ã‚’ä½¿ã£ã¦ã„ã‚‹ã®ã§ã€Misskeyã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ãŒå¤ã„ã¨å‹•ä½œã—ãªã„ã¨æ€ã„ã¾ã™ã€‚ãã®å ´åˆã¯ã€ã‚¢ãƒƒãƒ—ãƒ‡ãƒ¼ãƒˆã—ã¦ãã ã•ã„ã€‚

```aiscript
/// @ 0.17.0

@brainfxck(code) {
  var ptr = 0
  var mem = [0, null]
  var pc = 0
  var tmp = 0

  var loop_count= 0
  var loop_limit = 1000000

  var output = []

  loop {
    if pc >= code.len break

    match (code.pick(pc)) {
      '>' => eval {
        ptr = ptr + 1
        if mem[ptr] == null {
          mem[ptr] = 0
          mem.push(null)
        }
      }
      '<' => ptr = ptr - 1
      '+' => mem[ptr] = mem[ptr] + 1
      '-' => mem[ptr] = mem[ptr] - 1
      '.' => output.push(Str:from_codepoint(mem[ptr]))
      '[' =>
        if mem[ptr] == 0 {
          tmp = 1
          loop {
            if tmp <= 0 break
            pc = pc + 1
            if code.pick(pc) == '[' tmp = tmp + 1
            if code.pick(pc) == ']' tmp = tmp - 1
          }
        }
      ']' =>
        if mem[ptr] != 0 {
          tmp = 1
          loop {
            if tmp <= 0 break
            pc = pc - 1
            if code.pick(pc) == ']' tmp = tmp + 1
            if code.pick(pc) == '[' tmp = tmp - 1
          }
          pc = pc - 1
        }
    }

    loop_count = loop_count + 1
    if loop_count >= loop_limit {
      output.push("... Too many loops!")
      break
    }

    pc = pc + 1
  }

  output.join("")
}

var code = "++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++."

Ui:render([
  Ui:C:textarea({
    onInput: @(value) { code = value }
    default: code
    label: "Code"
  })
  Ui:C:button({
    text: "Run!"
    onClick: @() {
      var result = brainfxck(code)
      Ui:get("container").update({hidden: false})
      Ui:get("result").update({text: result})
    }
    primary: true
    rounded: true
  })
  Ui:C:container({
    children: [
      Ui:C:text({
        text: ""
      } "result")
    ]
    align: 'left'
    bgColor: '#000'
    fgColor: '#fff'
    font: 'monospace'
    borderWidth: 1
    borderColor: '#f00'
    padding: 5
    rounded: true
    hidden: true
  } "container")
])
```

ãã‚Œã«ã—ã¦ã‚‚ã€Misskeyã®AiScriptã¯ã€ã„ã„æ„Ÿã˜ã®UIã‚³ãƒ³ãƒãƒ¼ãƒãƒ³ãƒˆã‚‚å®Ÿè£…ã•ã‚Œã¦ã„ã‚‹ã—ã€ã‚„ã‚ã†ã¨æ€ãˆã°ã‹ãªã‚Šã„ã‚ã‚“ãªã“ã¨ãŒã§ããã†ã§ã€é¢ç™½ã„ã§ã™ã­ã€‚

## ãŠã‚ã‚Šã«

Fediverseã‚’æ”¯ãˆã‚‹ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã‚„å„ç¨®å®Ÿè£…ã€ã‚¨ã‚³ã‚·ã‚¹ãƒ†ãƒ ã‚‚ç´ æ™´ã‚‰ã—ã„ã§ã™ã—ã€Misskeyã®ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã¨ã—ã¦ã®å®Œæˆåº¦ã€æ©Ÿèƒ½ã®è±Šå¯Œã•ã‚‚ã™ã”ã„ã¨æ€ã„ã¾ã™ã€‚ã“ã‚Œã‹ã‚‰ã‚‚ã‚‚ã£ã¨éŠã³å€’ã—ã¦ã„ããŸã„ã¨æ€ã„ã¾ã™ã€‚
